cmake_minimum_required(VERSION 3.23 FATAL_ERROR)

option(LIBMD5RFC_BUILD_APPLICATION "Build LibMD5-RFC application." OFF)

set(HEADER_FILE "${CMAKE_CURRENT_SOURCE_DIR}/md5.h")

if(NOT EXISTS "${HEADER_FILE}")
	message(FATAL_ERROR "Header not found: '${HEADER_FILE}'.")
endif()

file(READ "${HEADER_FILE}" _HEADER_FILE_CONTENTS)
string(REGEX MATCH "#define[ \t]+LIBMD5RFC_VERSION[ \t]+\"([0-9]+\\.[0-9]+\\.[0-9]+)\"" _MATCH "${_HEADER_FILE_CONTENTS}")
set(LIBMD5RFC_VERSION "${CMAKE_MATCH_1}")

project(LibMD5-RFC VERSION ${LIBMD5RFC_VERSION} LANGUAGES C)

set(LIBMD5RFC_LIBRARY_NAME MD5)
set(LIBMD5RFC_APPLICATION_NAME md5sum)

set(INSTALL_TARGETS
	${LIBMD5RFC_LIBRARY_NAME}
)

set(CMAKE_POSITION_INDEPENDENT_CODE ON)

add_library(${LIBMD5RFC_LIBRARY_NAME}
	md5.c
)

add_library(${PROJECT_NAME}::${LIBMD5RFC_LIBRARY_NAME} ALIAS ${LIBMD5RFC_LIBRARY_NAME})

target_sources(${LIBMD5RFC_LIBRARY_NAME}
	PUBLIC
		FILE_SET HEADERS
		TYPE HEADERS
		BASE_DIRS
			${CMAKE_CURRENT_SOURCE_DIR}
		FILES
			md5.h
)

target_include_directories(${LIBMD5RFC_LIBRARY_NAME}
	PUBLIC
		$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>
		$<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>
)

target_compile_features(${LIBMD5RFC_LIBRARY_NAME}
	PUBLIC
		c_std_99
)

if(LIBMD5RFC_BUILD_APPLICATION)
	add_executable(${LIBMD5RFC_APPLICATION_NAME}
		md5main.c
	)

	set_property(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR} PROPERTY VS_STARTUP_PROJECT ${LIBMD5RFC_APPLICATION_NAME})

	target_compile_features(${LIBMD5RFC_APPLICATION_NAME}
		PUBLIC
			c_std_99
	)

	target_link_libraries(${LIBMD5RFC_APPLICATION_NAME}
		PRIVATE
			${LIBMD5RFC_LIBRARY_NAME}
	)
endif()

include(GNUInstallDirs)

set(CONFIG_INSTALL_DIR "${CMAKE_INSTALL_LIBDIR}/cmake/${PROJECT_NAME}")
set(GENERATED_DIR "${CMAKE_CURRENT_BINARY_DIR}/generated")
set(VERSION_CONFIG "${GENERATED_DIR}/${PROJECT_NAME}ConfigVersion.cmake")
set(PROJECT_CONFIG "${GENERATED_DIR}/${PROJECT_NAME}Config.cmake")
set(TARGETS_EXPORT_NAME "${PROJECT_NAME}Targets")
set(NAMESPACE "${PROJECT_NAME}::")

include(CMakePackageConfigHelpers)

write_basic_package_version_file(
	"${VERSION_CONFIG}" COMPATIBILITY SameMajorVersion
)

configure_package_config_file(
	"Config.cmake.in"
	"${PROJECT_CONFIG}"
	INSTALL_DESTINATION "${CONFIG_INSTALL_DIR}"
)

install(
	TARGETS ${INSTALL_TARGETS}
	EXPORT "${TARGETS_EXPORT_NAME}"
	LIBRARY DESTINATION "${CMAKE_INSTALL_LIBDIR}"
	ARCHIVE DESTINATION "${CMAKE_INSTALL_LIBDIR}"
	RUNTIME DESTINATION "${CMAKE_INSTALL_BINDIR}"
	FILE_SET HEADERS
		DESTINATION "${CMAKE_INSTALL_INCLUDEDIR}"
)

install(
	FILES "${PROJECT_CONFIG}" "${VERSION_CONFIG}"
	DESTINATION "${CONFIG_INSTALL_DIR}"
)

install(
	EXPORT "${TARGETS_EXPORT_NAME}"
	NAMESPACE "${NAMESPACE}"
	DESTINATION "${CONFIG_INSTALL_DIR}"
)
